namespace Am.Util.Parsers.Text {

    class TextParser(private var string: String) {
        import Am.Lang

        private var index: Int
        private var sb: StringBuilder = new StringBuilder()

        fun parseUntil(predicate: (c: Char) => Bool) : String {
            this.sb.clear()
            while(this.index < this.string.getLength()) {
                var c = this.string.characterAt(this.index.toUInt())
                if (!(predicate(c))) {
                    this.sb.addChar(c)
                    this.index++
                } else {
                    break
                }
            }
            return this.sb.toString()
        }

        fun skipUntil(predicate: (c: Char) => Bool): Bool {
            while(this.index < this.string.getLength()) {
                var c = this.string.characterAt(this.index.toUInt())
                if (!(predicate(c))) {
                    this.index++
                } else {
                    return true
                }
            }
            return false
        }

        fun hasMore(): Bool {
            return this.index < this.string.getLength()
        }

        fun peek(len: UInt) : String {
            return this.string.substring(this.index.toUInt(), len)
        }

        fun peekChar() : Char {
            return this.string.characterAt(this.index.toUInt())
        }

        fun skip(len: UInt) {
            this.index += len.toInt()
        }

    }
}