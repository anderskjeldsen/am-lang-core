namespace Am.IO
{
    native class File(private var filename: String)
    {
        import Am.Lang
        import Am.Collections

        static native fun getCurrentDirectory(): String

        static fun getCurrentDirectoryFile(): File {
            return new File(File.getCurrentDirectory())
        }

        fun listFiles(): File[] {
            var filenames = this.list()
            var files = new File[filenames.length()]
            var i = 0
            while(i < filenames.length()) {
                files[i] = new File(filenames[i])
                i++
            }
            return files
        }

        fun list(): String[] {
            var l = List.newList<String>()
            var l2 = List.newList<String>()
            this.listNative(this.filename, l)
            var endsWithColon = (this.filename.endsWith(":"))
            var endsWithSlash = (this.filename.endsWith("/"))
            if (endsWithColon || endsWithSlash) {
                each(l, name) {
                    l2.add(this.filename + name)
                }
            } else {
                each(l, name) {
                    l2.add(this.filename + "/" + name)
                }
            }

            return l2.toArray()
        }

        private native fun listNative(folderFilename: String, list: List<String>)

        native fun isDirectory(): Bool

        // File metadata methods
        native fun exists(): Bool
        native fun getSize(): Long
        native fun getLastModified(): Long
        native fun canRead(): Bool
        native fun canWrite(): Bool

        // File operations methods
        native fun delete(): Bool
        static fun delete(filename: String): Bool {
            var file = new File(filename)
            return file.delete()
        }
        static native fun createDirectory(path: String): Bool
        native fun copy(destination: String): Bool
        native fun move(destination: String): Bool

        fun getName(): String {
            var index = this.filename.lastIndexOf("/")
            if (index < 0) {
                index = this.filename.lastIndexOf(":")
            }
            if (index >= 0) {
                return this.filename.substring(index.toUInt() + 1UI, this.filename.getLength().toUInt())
            }
            return this.filename
        }

        override fun toString(): String {
            return this.filename
        }

    }

    native class TempFile(private var file: File)
    {
        import Am.Lang
        
        fun getFile(): File {
            return this.file
        }

        fun getPath(): String {
            return this.file.toString()
        }
    }
}
